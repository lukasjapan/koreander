buildscript {
    ext.kotlin_version = '1.+'

    repositories {
        jcenter()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
        classpath 'com.github.jengelman.gradle.plugins:shadow:4.0.3'
        classpath "com.netflix.nebula:nebula-publishing-plugin:9.2.0"
    }
}

plugins {
    id 'com.github.kt3k.coveralls' version "2.8.2"
    id "io.gitlab.arturbosch.detekt" version "1.0.0.RC6"
    id "com.jfrog.bintray" version "1.7.3"
}

def koreanderGroup = 'de.cvguy.kotlin'
def koreanderName = 'koreander'
def koreanderVersion = '0.0.8'

group koreanderGroup
version koreanderVersion

apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'jacoco'
apply plugin: 'maven'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: "nebula.source-jar"

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    pkg {
        repo = koreanderGroup
        name = koreanderName
        licenses = ['MIT']
        vcsUrl = "https://github.com/lukasjapan/koreander.git"
        version {
            name = koreanderVersion
            desc = 'A HTML template engine for Kotlin.'
        }
    }
    configurations = ['archives']
}

sourceCompatibility = 1.8

repositories {
    jcenter()
}

detekt {
    version = "1.0.0.RC6"
    profile("main") {
        input = "$projectDir/src/main/kotlin"
        config = "$projectDir/detekt.yml"
        filters = ".*test.*,.*/resources/.*,.*/tmp/.*"
    }
}

jacoco {
    toolVersion = '0.8.2'
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

coveralls {
    sourceDirs += ['src/main/kotlin']
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-script-runtime:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-script-util:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-compiler:$kotlin_version"
    compile "org.slf4j:slf4j-api:1.+"
    testCompile group: 'junit', name: 'junit', version: '4.+'
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}